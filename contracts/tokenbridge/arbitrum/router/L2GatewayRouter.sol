// SPDX-License-Identifier: Apache-2.0

/*
 * Copyright 2020, Offchain Labs, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

pragma solidity ^0.6.11;

import "../../libraries/GatewayRouter.sol";
import "../../ethereum/router/L1GatewayRouter.sol";

/**
 * @title Handles withdrawals from Ethereum into Arbitrum. Tokens are routered to their appropriate L2 gateway (Router itself also conforms to the Gateway interface).
 * @notice Router also serves as an L2-L1 token address oracle.
 */
contract L2GatewayRouter is GatewayRouter {
    function initialize(address _counterpartGateway) public virtual {
        GatewayRouter._initialize(_counterpartGateway);
    }

    function setGateway(address[] memory _l1Token, address[] memory _gateway)
        external
        virtual
        onlyCounterpartGateway
    {
        // counterpart gateway (L1 router) should never allow wrong lengths
        assert(_l1Token.length == _gateway.length);

        for (uint256 i = 0; i < _l1Token.length; i++) {
            l1TokenToGateway[_l1Token[i]] = _gateway[i];
            emit GatewaySet(_l1Token[i], _gateway[i]);
        }
    }

    function getGateway(address _l1Token) public view virtual override returns (address gateway) {
        gateway = l1TokenToGateway[_l1Token];
        // if no gateway is set, address(0) will cause a revert in this check
        require(gateway.isContract(), "NO_GATEWAY_DEPLOYED");
        return gateway;
    }

    function preTransferHook() internal virtual override {
        // continue;
    }

    function isCounterpartGateway() internal view virtual override returns (bool) {
        return msg.sender == counterpartGateway;
    }
}
